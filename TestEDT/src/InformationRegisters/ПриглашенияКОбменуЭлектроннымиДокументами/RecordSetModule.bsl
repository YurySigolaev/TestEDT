
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ЭтотОбъект.Количество() Тогда
		ПроверитьУникальностьПриглашений(Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	КонтрагентыИзПриглашений = Новый Массив;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		Если ЗначениеЗаполнено(Запись.Контрагент)
			И КонтрагентыИзПриглашений.Найти(Запись.Контрагент) = Неопределено Тогда
			
			КонтрагентыИзПриглашений.Добавить(Запись.Контрагент);
		КонецЕсли;
	КонецЦикла;

	Если КонтрагентыИзПриглашений.Количество() Тогда
		РаботаСАбонентамиЭДО.ОбновитьСостоянияКонтрагентов(КонтрагентыИзПриглашений);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
	
#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьУникальностьПриглашений(Отказ)
	
	ПроверяемыеПриглашения = ЭтотОбъект.Выгрузить(, "Ключ,ИдентификаторОрганизации,ИдентификаторКонтрагента");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПроверяемыеПриглашения.Ключ КАК Ключ,
		|	ПроверяемыеПриглашения.ИдентификаторОрганизации КАК ИдентификаторОрганизации,
		|	ПроверяемыеПриглашения.ИдентификаторКонтрагента КАК ИдентификаторКонтрагента
		|ПОМЕСТИТЬ ВТ_ПроверяемыеПриглашения
		|ИЗ
		|	&ПроверяемыеПриглашения КАК ПроверяемыеПриглашения
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ключ,
		|	ИдентификаторОрганизации,
		|	ИдентификаторКонтрагента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПриглашенияНаИдентификаторы.Ключ КАК КлючСуществующего,
		|	ВТ_ПроверяемыеПриглашения.Ключ КАК КлючЗаписываемого,
		|	ПриглашенияНаИдентификаторы.ИдентификаторОрганизации,
		|	ПриглашенияНаИдентификаторы.ИдентификаторКонтрагента
		|ИЗ
		|	ВТ_ПроверяемыеПриглашения КАК ВТ_ПроверяемыеПриглашения
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПриглашенияКОбменуЭлектроннымиДокументами КАК ПриглашенияНаИдентификаторы
		|		ПО ВТ_ПроверяемыеПриглашения.ИдентификаторОрганизации = ПриглашенияНаИдентификаторы.ИдентификаторОрганизации
		|		И ВТ_ПроверяемыеПриглашения.ИдентификаторКонтрагента = ПриглашенияНаИдентификаторы.ИдентификаторКонтрагента
		|ГДЕ
		|	ВТ_ПроверяемыеПриглашения.Ключ <> ПриглашенияНаИдентификаторы.Ключ
		|	И ВТ_ПроверяемыеПриглашения.ИдентификаторОрганизации <> """"
		|	И ВТ_ПроверяемыеПриглашения.ИдентификаторКонтрагента <> """"";
	
	Запрос.УстановитьПараметр("ПроверяемыеПриглашения", ПроверяемыеПриглашения);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	ШаблонТекстаОшибки = 
		НСтр("ru = 'Приглашение с идентификатором организации: %1 и идентификатором контрагента: %2 уже существует.
									|Ключ существующего приглашения: %3, ключ записываемого приглашения: %4'");
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ТекстОшибки = СтрШаблон(ШаблонТекстаОшибки,
			ВыборкаДетальныеЗаписи.ИдентификаторОрганизации, ВыборкаДетальныеЗаписи.ИдентификаторКонтрагента,
			ВыборкаДетальныеЗаписи.КлючСуществующего, ВыборкаДетальныеЗаписи.КлючЗаписываемого);
	
		КлючПриглашения = ПриглашенияЭДОКлиентСервер.НовыйКлючПриглашения();
		КлючПриглашения.Ключ = ВыборкаДетальныеЗаписи.КлючСуществующего;
		КлючПриглашения.ИдентификаторОрганизации = ВыборкаДетальныеЗаписи.ИдентификаторОрганизации;
		КлючЗаписиПриглашения = ПриглашенияЭДО.КлючЗаписиПриглашения(КлючПриглашения);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, КлючЗаписиПриглашения);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
	
#КонецЕсли
