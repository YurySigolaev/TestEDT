
#Область СлужебныеПроцедурыИФункции

Функция СообщениеОбОтсутствииПечатныхФормДляВнутреннегоЭДО() Экспорт
		
	Возврат НСтр("ru = 'Для документа не предусмотрено печатных форм.
			|Для использования ЭДО необходимо подключить внешние печатные формы.'");
	
КонецФункции

Процедура УстановитьСвойстваПоляМаршрутПодписанияДляВнутреннегоЭДО(ПолеФормы, ВидЭлектроннойПодписи, Организация,
	РазрешитьМаршрутУказыватьПриСоздании = Истина) Экспорт
	
	ФиксированныеНастройки = Новый НастройкиКомпоновкиДанных;
	ГруппаОтбораИли = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ФиксированныеНастройки.Отбор, НСтр("ru = 'Выбор маршрута внутреннего ЭДО'"),
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
		
	ГруппаОтбораИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ГруппаОтбораИли, НСтр("ru = 'Схема подписания по правилам'"),
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаОтбораИ, "ВидПодписи",
		ВидЭлектроннойПодписи, ВидСравненияКомпоновкиДанных.Равно,, Истина);
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаОтбораИ, "Организация",
		Организация, ВидСравненияКомпоновкиДанных.Равно,, Истина);
			
	РазрешенныеМаршруты = Новый Массив;	
			
	Если РазрешитьМаршрутУказыватьПриСоздании Тогда
		РазрешенныеМаршруты.Добавить(МаршрутыПодписанияБЭДКлиентСервер.МаршрутУказыватьПриСоздании());
	КонецЕсли;	
	
	Если ВидЭлектроннойПодписи = ПредопределенноеЗначение("Перечисление.ВидыЭлектронныхПодписей.УсиленнаяКвалифицированная") Тогда
		РазрешенныеМаршруты.Добавить(МаршрутыПодписанияБЭДКлиентСервер.МаршрутОднойДоступнойПодписью());
	КонецЕсли;	

	Если ЗначениеЗаполнено(РазрешенныеМаршруты) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаОтбораИли, "Ссылка",
			РазрешенныеМаршруты, ВидСравненияКомпоновкиДанных.ВСписке,, Истина);
	КонецЕсли;

	МассивПараметров = Новый Массив;
	ПараметрВыбора = Новый ПараметрВыбора("ФиксированныеНастройки", ФиксированныеНастройки);
	МассивПараметров.Добавить(ПараметрВыбора);
	
	ПолеФормы.ОтображениеКнопкиВыбора = ОтображениеКнопкиВыбора.ОтображатьВПолеВвода;
	ПолеФормы.КнопкаВыбора = Истина;
	ПолеФормы.КнопкаВыпадающегоСписка = Ложь;
	ПолеФормы.КнопкаСоздания = Ложь;
	ПолеФормы.КнопкаОчистки = Истина;
	ПолеФормы.РедактированиеТекста = Ложь;
	ПолеФормы.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
		
КонецПроцедуры

#КонецОбласти